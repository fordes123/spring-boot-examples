plugins {
    id "idea"
    id "java"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "${springDependencyManagementVersion}"
}

allprojects {
    group "org.fordes"
    version "1.0.0-SNAPSHOT"

    apply plugin: "idea"
    apply plugin: "java"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }
}

subprojects {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter"
        runtimeOnly "org.springframework.boot:spring-boot-devtools"

        testImplementation "org.springframework.boot:spring-boot-starter-test"

        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    }

    sourceSets {
        main {
            resources {
                srcDirs += '../resources'
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
}